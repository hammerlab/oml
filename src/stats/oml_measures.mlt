(*
   Copyright 2015:
     Carmelo Piccione <carmelo.piccione@gmail.com>
     Leonid Rozenberg <leonidr@gmail.com>

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*)

open Test_utils

let () =
  let add_simple_test = Test.add_simple_test_group __MODULE__ in
  let add_random_test
    ?title ?nb_runs ?nb_tries ?classifier
    ?reducer ?reduce_depth ?reduce_smaller ?random_src gen f spec =
    Test.add_random_test_group __MODULE__
      ?title ?nb_runs ?nb_tries ?classifier
      ?reducer ?reduce_depth ?reduce_smaller ?random_src gen f spec
  in
  add_random_test
    ~title:"Normal kl divergence approaches infinity as means diverge"
    Gen.(zip2 (make_float 0.0 1e8) (make_float 0.0 1e8))
    (fun (p_mean, q_mean) ->
      let p_sigma = max 0.1 (sqrt @@ abs_float p_mean) in
      let q_sigma = p_sigma in
      let kldiv q_mean = normal_kl_divergence
        ~p_mean ~q_mean ~p_sigma ~q_sigma in
      List.fold_left [0.6;0.4;0.25;0.1]
        ~f:(fun (res, kl, l) frac ->
            let delta = (p_mean -. res) *. frac in
            let res' = res +. delta in
            let kl' = kldiv res' in
            (res', kl', (res', kl, kl')::l))
        ~init:(q_mean, infinity, [])
      |> fun (_, _, l) ->
        List.for_all ~f:(fun (_res, kl, kl') -> kl' <= kl) l)
    Spec.([just_postcond_pred is_true]);

  add_random_test
    ~title:"Normal kl divergence approaches infinity as sigmas diverge"
    Gen.(zip2 (make_float 0.01 1e8) (make_float 0.01 1e8))
    (fun (p_sigma, q_sigma) ->
      let p_mean = p_sigma *. p_sigma in
      let q_mean = p_mean in
      let kldiv q_sigma = normal_kl_divergence
        ~p_mean ~q_mean ~p_sigma ~q_sigma in
      List.fold_left [0.6;0.4;0.25;0.1]
        ~f:(fun (res, kl, l) frac ->
              let delta = (p_sigma -. res) *. frac in
              let res' = res +. delta in
              let kl' = kldiv res' in
              (res', kl', (res', kl, kl')::l))
        ~init:(q_sigma, infinity, [])
      |> fun (_, _, l) ->
        List.for_all ~f:(fun (_res, kl, kl') -> kl' <= kl) l)
    Spec.([just_postcond_pred is_true]);

  add_simple_test
    ~title:"Discrete KL divergence is always positive"
    (fun () ->
      let fair_die = Array.init 6 (fun i -> i + 1, 1. /. 6.) |> Array.to_list in
      let not_fair = [ (1, 0.1) ; (2, 0.1); (3, 0.1); (4, 0.1); (5, 0.1); (6, 0.5) ] in
      let kld =  discrete_kl_divergence ~p:fair_die ~q:not_fair in
      Assert.equalf kld 0.242585971693640517);

  ()
